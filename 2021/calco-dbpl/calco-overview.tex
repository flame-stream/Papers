\subsection{Contracts interface}

Graph operations can be user-defined, so we cannot deduce their properties automatically to transform the graph safely.
So we need to ask the user to specify such properties manually.
Such specifications we call contracts.

There are two kinds of contracts: input and output.
Input contract specifies requirements that input data should satisfy, output contract defines how an operation changes the input data.
We call information about the data attributes and properties a {\em data scheme}.
Input contract is a data type with the {\em match} function defined: it takes an input data scheme, input contract, and returns boolean if an input data scheme satisfies an input contract.

In this paper, we demonstrate all notions and examples in Haskell, the prototype~\footnote{https://github.com/flame-stream/halco} is also implemented in Haskell. The type class for input contract is defined as follows:

\begin{lstlisting}[language=Haskell]
class InCont s i where
  match :: s -> i -> Bool
\end{lstlisting}

Graph nodes can be of three types: data sources, one-arity operations, and two-arity operations.
So there are three definitions of output contracts.
The output contract of the data source should have a function to get an output data scheme from it.

\begin{lstlisting}[language=Haskell]
class OutCont s o where
  toScheme :: o -> s
\end{lstlisting}

Output contract of the one-arity operation defines, how the operation changes the input data, hence there should be a function {\em update1} that takes an input data scheme, an output contract and returns an output data scheme:

\begin{lstlisting}[language=Haskell]
class OutCont1 s o where
  update1 :: s -> o -> s
\end{lstlisting}

Output contract of the two-arity operation defines how the node transforms the data of two sources:

\begin{lstlisting}[language=Haskell]
class OutCont2 s o where
  update2 :: (s, s) -> o -> s
\end{lstlisting}

\subsection{Contracts implementation}

Input and output data can be described using two sorts of information: attributes that every data element has and properties of this attributes.
Properties are denoted just as strings, like {\em "reliablePerson"} or {\em "popularItem"}.

\begin{lstlisting}[language=Haskell]
type Attr = String
type Prop = String

data Scheme = Scheme
  { attrs :: Set Attr
  , props :: Set Prop }

empty :: Scheme
empty = Scheme { attrs = Set.empty
               , props = Set.empty }

union :: Scheme -> Scheme -> Scheme
union s1 s2 = Scheme
  { attrs = attrs s1 `Set.union` attrs s2
  , props = props s1 `Set.union` props s2 }
\end{lstlisting}

Input contract consists of three sets: 
attributes that should exist in the input data scheme,
properties that should exist in the input data scheme
and properties that should not exist in the input data scheme.

\begin{lstlisting}[language=Haskell]
data InContImpl = InContImpl
  { attrsI  :: Set Attr
  , propsI  :: Set Prop
  , propsI' :: Set Prop }

instance InCont Scheme InContImpl where
  match :: Scheme -> InContImpl -> Bool
  match s i =
       attrsI i `Set.isSubsetOf` attrs s
    && propsI i `Set.isSubsetOf` props s
    && propsI' i `Set.disjoint`  props s
\end{lstlisting}

Output contract consists of two sets: 
the attributes that operation adds to the each data element
and 
the new properties of the data elements (e.g., filter operation adds property "filteredByCategory").

\begin{lstlisting}[language=Haskell]
data OutContImpl = OutContImpl
  { attrsO :: Set Attr
  , propsO :: Set Prop }

instance OutCont Scheme OutContImpl where
  toScheme :: OutContImpl -> Scheme
  toScheme = update1 Scheme.empty

instance OutCont1 Scheme OutContImpl where
  update1 :: Scheme -> OutContImpl -> Scheme
  update1 s o = Scheme
    { attrs = attrs s `Set.union` attrsO o
    , props = props s `Set.union` propsO o }

instance OutCont2 Scheme OutContImpl where
  update2 :: 
    (Scheme, Scheme) -> OutContImpl -> Scheme
  update2 (s1, s2) = 
    update1 (s1 `Scheme.union` s2)
\end{lstlisting}

\subsection{CGraph}

{\em Environment} is a set of {\em nodes}, annotated with {\em input} and {\em output contracts}.
Nodes can be data sources, one-arity operations, and two-arity operations.

\begin{lstlisting}[language=Haskell]
data Env i o o1 o2 = Env
  { sources :: Map NodeName o
  , ops1 :: Map NodeName (i, o1)
  , ops2 :: Map NodeName (i, i, o2) }
\end{lstlisting}

Some nodes produce particular side effects that form the result of the running graph (writes data to the storage, displays some statistics on the dashboard, etc.).
Such nodes should exist in all generated execution graphs.
We call the nodes that produce such side effects a {\em graph semantics}.

\begin{lstlisting}[language=Haskell]
type Semantics = Set NodeName
\end{lstlisting}

CGraph is simply a pair of environment and semantics.

\begin{lstlisting}[language=Haskell]
type CGraph i o o1 o2 = 
  (Env i o o1 o2, Semantics)
\end{lstlisting}

Graphs with satisfied contracts that include all semantics nodes form the desired set of equivalent graphs. 
The implementation of the execution graphs generation algorithm is rather effective despite the brute force approach because graphs that do not satisfy contracts can be ejected. 
The algorithm is exponential in the size of the semantics set, which is often small enough.
The algorithm implementation details are discussed in the next section.
